apply plugin: "c"

def cpus =  file("./cpus").list()
def syntaxes =  file("./syntax").list()

for (cpu in cpus) {
    for (syntax in syntaxes) {
        final componentName = "vasm${cpu}_${syntax}"
        final cpuDir = "./cpus/$cpu"
        final syntaxDir = "./syntax/$syntax"
        model {
            platforms {
                x86 {
                    architecture "x86"
                }
                x86_64 {
                    architecture "x86_64"
                }
            }
            components {
                "$componentName"(NativeExecutableSpec) {
                    targetPlatform "x86"
                    targetPlatform "x86_64"
                    sources {
                        c {
                            source {
                                srcDirs ".", cpuDir, syntaxDir
                                include "*.c"
                                exclude "vobjdump.c"
                            }
                            exportedHeaders {
                                srcDirs ".", cpuDir, syntaxDir
                                include "*.h"
                            }
                        }
                    }
                    binaries.all {
                        cCompiler.define "OUTAOUT"
                        cCompiler.define "OUTBIN"
                        cCompiler.define "OUTELF"
                        cCompiler.define "OUTHUNK"
                        cCompiler.define "OUTSREC"
                        cCompiler.define "OUTTOS"
                        cCompiler.define "OUTVOBJ"
                        cCompiler.define "OUTXFIL"
                        cCompiler.define "OUTIHEX"
                        //cCompiler.define "WINDOWS_IGNORE_PACKING_MISMATCH"
                        if ((toolChain in Gcc) || (toolChain in Clang)) {
                            cCompiler.define "UNIX"
                            cCompiler.args "-O2"
                            linker.args "-lm"
                        } else if (toolChain in VisualCpp) {
                            cCompiler.args "/Fo", "/nologo", "/O2", "/MT", "/c", "/wd4996" 
                            linker.args "/NOLOGO"
                        }
                    }
                }
            }
        }
    }
}

